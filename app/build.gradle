apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'findbugs'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'packer'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOL_VERSION

    defaultConfig {
        applicationId "com.teambition.talk"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 201603210
        versionName "3.0.3"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true

        multiDexEnabled true

    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    String[] lintIgnoreIds = ['NestedScrolling', 'ResourceAsColor', 'ContentDescription']
    String[] lintWarningIds = ['InvalidPackage', 'StringFormatInvalid', 'MissingTranslation']
    lintOptions {
        disable lintIgnoreIds
        warning lintWarningIds
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    signingConfigs {
        release {
//            storeFile file("Your keystore")
//            storePassword RELEASE_STORE_PASSWORD
//            keyAlias RELEASE_KEY_ALIAS
//            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "int", "BUILD_TYPE_INT", "0"
            buildConfigField "boolean", "BUILD_FOR_FIR", BUILD_FOR_FIR
            resValue "string", "umeng_app_key", ""
            resValue "string", "baidu_api_key", ""
            resValue "string", "jianliao_host", "jianliao.com"
            resValue "string", "tps_key", "EJgUfwYbe"
        }

        beta {
            debuggable true
            signingConfig signingConfigs.release
            buildConfigField "int", "BUILD_TYPE_INT", "1"
            buildConfigField "boolean", "BUILD_FOR_FIR", "false"
            resValue "string", "umeng_app_key", ""
            resValue "string", "baidu_api_key", ""
            resValue "string", "jianliao_host", "jianliao.com"
            resValue "string", "tps_key", "EJgUfwYbe"
        }

        debug {
            buildConfigField "int", "BUILD_TYPE_INT", "2"
            buildConfigField "boolean", "BUILD_FOR_FIR", "false"
            resValue "string", "umeng_app_key", ""
            resValue "string", "baidu_api_key", ""
            resValue "string", "jianliao_host", "dev.talk.ai"
            resValue "string", "tps_key", "EJ7MrS-i"
        }

    }

    productFlavors {
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
        }
        prod {
            minSdkVersion 14
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    output.outputFile.name.replace("app", "Talk-${variant.versionName}"))
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res']
            jniLibs.srcDirs = ['libs']
        }
    }


}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':library:chips')
    compile project(':library:emojicon')
    compile project(':library:TalkDialog')
    compile project(':library:engine.io-client')
    compile project(':library:JniBitmapOperationsLibrary')
    compile project(':library:yuv')
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.teambition.common:library:1.0.1'
    compile 'com.makeramen:roundedimageview:2.0.1'
    compile 'com.joooonho:selectableroundedimageview:1.0.1'
    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.squareup.retrofit:retrofit:1.7.0'
    compile ('com.squareup.retrofit:converter-simplexml:1.7.0'){
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.netflix.rxjava:rxjava-android:0.20.6'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'com.github.castorflex.smoothprogressbar:library-circular:1.1.0'
    compile 'com.getbase:floatingactionbutton:1.10.0'
    compile 'com.rengwuxian.materialedittext:library:1.4.3'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
    compile 'com.umeng.analytics:analytics:5.6.1'
    compile 'com.journeyapps:zxing-android-embedded:3.0.1@aar'
    compile 'com.google.zxing:core:3.2.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:7.0.9'
    compile 'se.emilsjolander:stickylistheaders:2.5.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.yqritc:recyclerview-flexibledivider:1.2.6'
    compile "org.parceler:parceler-api:1.0.3"
    apt "org.parceler:parceler:1.0.3"
    compile 'io.realm:realm-android:0.87.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.mixpanel.android:mixpanel-android:4.7.0@aar'
    compile 'com.github.amlcurran.showcaseview:library:5.4.0'
    compile 'com.tencent.bugly:crashreport:1.2.9'
    compile 'com.mcxiaoke.gradle:packer-helper:1.0.4'
    debugCompile 'com.facebook.stetho:stetho:1.1.0'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.+'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2') {
        exclude module: 'support-annotations'
    }
}

task customFindbugs (type: FindBugs, dependsOn: assembleDebug) {

    description 'Run findbugs'
    group 'verification'

    if (BUILD_FLAVORS) {
        classes = fileTree('build/intermediates/classes/teambition/debug')
    } else {
        classes = fileTree('build/intermediates/classes/debug')
    }
    source = fileTree('src/main/java')
    classpath = files()

    effort = 'max'
    ignoreFailures = true
    reportLevel = "low"

    excludeFilter = file("./findbugsExclude.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}
